*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        upload_raw_artifact
Test Template     Upload Raw Artifact Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    RespositoryName        File                   FileName      Directory
Upload Raw Artifact while providing all the fields
                      [Tags]    main_scenarios
                      OK        ${Respository_Name}    ${Upload_Artifact}     robot.jar     ${Dir}

Upload Raw Artifact while asserting File Name as Optional Field
                      OK        ${Respository_Name}    ${Upload_Artifact}     ${EMPTY}      ${Dir}

Upload Raw Artifact while not providing extenstion in File Name
                      OK        ${Respository_Name}    ${Upload_Artifact}     automation    ${Dir}

Upload Raw Artifact with File Name extenstion other than original
                      OK        ${Respository_Name}    ${Upload_Artifact}     test.zip      ${Dir}

Upload Raw Artifact with Non-Existing Repository
                      NOK       Invalid                ${Upload_Artifact}     ${EMPTY}      ${Dir}

Upload Raw Artifact with Non-Existing Path
                      NOK       ${Respository_Name}    Invalid                ${EMPTY}      ${Dir}

Upload Raw Artifact with Non-Existing File
                      NOK       ${Respository_Name}    ${EXECDIR}\\abc.jar    ${EMPTY}      ${Dir}

Upload Raw Artifact while providing directory only for File Input
                      NOK       ${Respository_Name}    ${EXECDIR}             ${EMPTY}      ${Dir}

Assert Repository Name as Mandatory Field
                      NOK       ${EMPTY}               ${Upload_Artifact}     robot.jar     ${Dir}

Assert File as Mandatory Field
                      NOK       ${Respository_Name}    ${EMPTY}               robot.jar     ${Dir}

Assert Destination as Mandatory Field
                      NOK       ${Respository_Name}    ${Upload_Artifact}     robot.jar     ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "UPLOAD RAW ARTIFACT"*******
    Log    *******Connect to AE*******
    Connect AE

Upload Raw Artifact Template
    [Arguments]    ${OK_NOK}    ${RespositoryName}    ${File}    ${FileName}    ${Directory}
    [Documentation]    This test uploads a raw artifact on Nexus repository
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${RespositoryName}: This field specifies name of the repository in which you want to upload artifact
    ...    -${File}: This field specifies file you want to upload with full path.
    ...    -${FileName}: This field specifies name to your file.
    ...    -${Directory}: This field specifies the destination directory in which file will be uploaded.
    Generate String
    Init Action    ${_UPLOAD_RAW_ARTIFACT_ACTION}
    Nexus Authentication
    Run Keyword If    '${RespositoryName}' == 'Invalid'    Action Set    &UC4RB_NXS_REPOSITORY#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_REPOSITORY#    ${RespositoryName}
    Run Keyword If    '${File}' == 'Invalid'    Action Set    &UC4RB_NXS_FILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_FILE#    ${File}
    Action Set    &UC4RB_NXS_FILE_NAME#    ${FileName}
    Action Set    &UC4RB_NXS_DIRECTORY#    ${Directory}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "UPLOAD RAW ARTIFACT"*******
