*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        retrieve_maven_artifact
Test Template     Retrieve Maven Artifact Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    RespositoryName        GroupID        ArtifactID        BaseVersion        Extension    Classifier    TargetFolder        TargetFile
Retrieve Maven Artifact while providing all the fields
                      [Tags]    main_scenarios
                      OK        ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${Base_Version}    jar          dev           ${Target_Folder}    ${File_Name}

Retrieve Maven Artifact while providing all the fields and Target File name already exist.
                      OK        ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      ${Target_Folder}    ${File_Name}

Retrieve Maven Artifact with Target file name having no extension
                      OK        ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      ${Target_Folder}    MytargetFile

Retrieve Maven Artifact with target Folder as Empty
                      OK        ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Retrieve Maven Artifact with Non-Existing Repository
                      NOK       Invalid                ${Group_ID}    ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Retrieve Maven Artifact with Non-Existing Group ID
                      NOK       ${Respository_Name}    Invalid        ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Retrieve Maven Artifact with Invalid artifact ID
                      NOK       ${Respository_Name}    ${Group_ID}    Invalid           ${Base_Version}    ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Retrieve Maven Artifact with Invalid base Version
                      NOK       ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    Invalid            ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Retrieve Maven Artifact with extension
                      NOK       ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${Base_Version}    Invalid      ${EMPTY}      ${EMPTY}            ${File_Name}

Retrieve Maven Artifact with Invalid classifier
                      NOK       ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${Base_Version}    ${EMPTY}     Invalid       ${EMPTY}            ${File_Name}

Retrieve Maven Artifact with Invalid Target Folder
                      NOK       ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      Invalid             ${File_Name}

Assert Repository Name as Mandatory Field
                      NOK       ${EMPTY}               ${Group_ID}    ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Assert Group ID as Mandatory Field
                      NOK       ${Respository_Name}    ${EMPTY}       ${Artifact_ID}    ${Base_Version}    ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Assert Artifact ID as Mandatory Field
                      NOK       ${Respository_Name}    ${Group_ID}    ${EMPTY}          ${Base_Version}    ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Assert Base Version as Mandatory Field
                      NOK       ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${EMPTY}           ${EMPTY}     ${EMPTY}      ${EMPTY}            ${File_Name}

Assert Target File as Mandatory Field
                      NOK       ${Respository_Name}    ${Group_ID}    ${Artifact_ID}    ${EMPTY}           ${EMPTY}     ${EMPTY}      ${EMPTY}            ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "RETRIEVE MAVEN ARTIFACT"*******
    Log    *******Connect to AE*******
    Connect AE

Retrieve Maven Artifact Template
    [Arguments]    ${OK_NOK}    ${RespositoryName}    ${GroupID}    ${ArtifactID}    ${BaseVersion}    ${Extension}
    ...    ${Classifier}    ${TargetFolder}    ${TargetFile}
    [Documentation]    This test retrieves a maven artifact on Nexus repository
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${RespositoryName}: This field specifies name of the repository from which you want to retrieve artifact.
    ...    -${GroupID}: This field Specifies group id of the component.
    ...    -${ArtifactID}: This field Specifies id of the artifact to be retrieved.
    ...    -${BaseVersion}: This field Specifies extension of the component..
    ...    -${Classifier}: This field Specifies classifier of the component.
    ...    -${TargetFolder}: This field Specifies target folder in which artifact will be downloaded. If empty default temporary folder is used
    ...    -${TargetFile}: This field Specifies target file in which artifact will be downloaded..
    Generate String
    Init Action    ${_RETRIEVE_MAVEN_ARTIFACT_ACTION}
    Nexus Authentication
    Run Keyword If    '${RespositoryName}' == 'Invalid'    Action Set    &UC4RB_NXS_REPOSITORY#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_REPOSITORY#    ${RespositoryName}
    Run Keyword If    '${GroupID}' == 'Invalid'    Action Set    &UC4RB_NXS_GROUP_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_GROUP_ID#    ${GroupID}
    Run Keyword If    '${ArtifactID}' == 'Invalid'    Action Set    &UC4RB_NXS_ARTIFACT_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_ARTIFACT_ID#    ${ArtifactID}
    Run Keyword If    '${BaseVersion}' == 'Invalid'    Action Set    &UC4RB_NXS_VERSION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_VERSION#    ${BaseVersion}
    Run Keyword If    '${Extension}' == 'Invalid'    Action Set    &UC4RB_NXS_EXTENSION#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_EXTENSION#    ${Extension}
    Run Keyword If    '${Classifier}' == 'Invalid'    Action Set    &UC4RB_NXS_CLASSIFIER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_CLASSIFIER#    ${Classifier}
    Run Keyword If    '${TargetFolder}' == 'Invalid'    Action Set    &UC4RB_NXS_TARGET#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_NXS_TARGET#    ${TargetFolder}
    Action Set    &UC4RB_NXS_TARGET_FILE#    ${TargetFile}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_NXS_ARCHIVE_PATH#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_NXS_ARCHIVE_PATH#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_NXS_FILE_NAME#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_NXS_FILE_NAME#

Teardown
    Log    *******End Test "RETRIEVE MAVEN ARTIFACT"*******
